/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'm.winkelmann' at '14.11.16 17:53' with Gradle 3.1
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.1/userguide/tutorial_java_projects.html
 */


buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
    }
}

// Apply the java plugin to add support for ...
plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.9.6"
}

group = 'de.esailors.gradle.plugins'
version = '1.0.0-SNAPSHOT'
project.ext.baseVersion = project.version.replace("-SNAPSHOT", "")
description = 'Plugin to handle everything related with the PhraseApp API.'

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.21'

    compile 'org.projectlombok:lombok:1.16.10'
    compile 'com.mytaxi.apis:phrase-java-client:1.0.1'
    compile 'org.springframework:spring-web:3.2.15.RELEASE'
    testCompile 'junit:junit:4.12'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc {
    exclude "**/internal/**"
    options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
    title "PhraseApp Gradle plugin"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

task publishLocal(type: Upload) {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            repository(url: uri('repo'))
        }
    }
}

pluginBundle {
    website = 'http://www.gradle.org/'
    vcsUrl = 'https://github.com/eSailors/phraseapp-gradle-plugin'

    plugins {
        phraseAppPlugin {
            id = 'de.esailors.gradle.plugins.phraseapp'
            displayName = 'PhraseApp Gradle Plugin'
            description = 'Synchronizing your PhraseApp strings for a project.'
            tags = ['esailors', 'phraseapp', 'phrase', 'i18n', 'translation', 'plugin']
            version = '1.0.0'
        }
    }
}